plugins {
    id 'com.github.triplet.play' version '2.7.5'
}

apply plugin: 'com.android.application'

def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.3'

    defaultConfig {
        applicationId "com.sailu.aliendream"
        minSdkVersion 17
        targetSdkVersion 29
        versionCode 1 // Will be incremented automatically on release
        versionName getVersionName()
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    signingConfigs {
        release {
            def keystorePropertiesFile = rootProject.file("../AppSignature/keystore.properties")
            if (keystorePropertiesFile.exists()) {
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

                keyAlias keystoreProperties.keyAlias
                keyPassword keystoreProperties.keyPassword
                storeFile file(keystoreProperties.storeFile)
                storePassword keystoreProperties.storePassword
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    implementation 'com.google.code.gson:gson:2.8.6'

    implementation 'com.google.android.exoplayer:exoplayer-core:2.11.6'

    implementation 'androidx.leanback:leanback:1.0.0'
    implementation 'androidx.leanback:leanback-preference:1.0.0'

    testImplementation 'junit:junit:4.12'
}

preBuild.doFirst {
    // Fetch playlist files
    exec {
        executable "./fetch_playlists.sh"
    }
}

clean.doFirst {
    // Delete playlists
    delete fileTree(dir:'src/main/res/raw/', include: 'tvos**.json')
}

play {
    serviceAccountCredentials = rootProject.file("../AppSignature/play-store.json")
    track = 'beta'
    resolutionStrategy = "auto"
}
